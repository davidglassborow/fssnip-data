module List =
  let update pred map list =
    list
    |> List.map
        (fun p ->
            if pred p
            then map p
            else p)

module Array =
  let rand = new System.Random()

  let swap (a: _[]) x y =
    let tmp = a.[x]
    a.[x] <- a.[y]
    a.[y] <- tmp

  // shuffle an array (in-place)
  let shuffle a =
    Array.iteri (fun i _ -> swap a i (rand.Next(i, Array.length a))) a
    a

type Suite =
  A | B | C | D

type Card =
  { value: int
    suite: Suite }
  static member create suite value  =
    { value = value
      suite = suite }

type PlayerId = int

type PlayerCards = (PlayerId*(Card list))

type Boont =  NoBoont | Boont | Hamsa | Ashra

type ScorePoint = int

type Score = PlayerId * ScorePoint

type Deck = Card []

let cardValue card = card.value

module Board =

  type Player =
    { id : PlayerId
      cards: Card list
      foodCount: int }

  type Model =
    { deck : Deck
      community: Card list
      players: Player list }


  let isNextValue (v1,v2) =
    (v1 = 7 && v2 = 10) || (v1 + 1 = v2)

  let allCards =
    [A;B;C;D]
    |> List.fold
      (fun acc suite ->
        let cards =
          [1..7]@[10..12]
          |> List.map (Card.create suite)
        acc @ cards
      ) []

  let newDeck () =
    allCards
      |> Array.ofList
      |> Array.shuffle

  let empty =
    { deck = Array.empty
      community = List.empty
      players = List.empty }

  let create community players =
    { deck = newDeck ()
      community = community
      players = players }

  let deal (model : Model) =
    let updatePlayer player cards =
      {player with cards = cards |> List.ofArray}

    let (players',deck') =
      model.players
      |> List.fold
        (fun (arr,curDeck) player ->
          let playerCards = (curDeck |> Array.take 3)
          let player' = updatePlayer player playerCards
          let arr' = player'::arr
          let deck' = curDeck |> Array.skip 3
          (arr',deck')
        ) ([], model.deck)
    { model with
        deck = deck'
        players = players' }

  let addPlayerCard (board:Model) playerId card =

    let matchingValues =
      board.community
      |> List.map cardValue
      |> List.sort
      |> List.skipWhile ((<>)card.value)
      |> List.pairwise
      |> List.takeWhile isNextValue
      |> List.collect (fun p -> [fst p;snd p])
      |> List.distinct

    let (community',matchingCards) =
      board.community
      |> List.fold
        (fun (cs,ms) c ->
          if List.contains c.value matchingValues
          then (cs,c::ms)
          else (c::cs,ms)
        ) ([],[])

    let players' =
      board.players
      |> List.update
        ((=)playerId)
        (fun p ->
          { p with
              foodCount = p.foodCount + (matchingCards |> List.length)
              cards = p.cards |> List.filter ((=)card)
           }
         )

    { board with
        players = players'
        community = community' }

module Turn =
  type Model = Turn of int*int

  let current (Turn(_,cur)) = cur
  let next (Turn (count,cur)) =
    Turn (count, (cur + 1) % count)
  let create count start =
    Turn (count,start)

module Boonts =
  type Model =
    { lastCard : (Card*PlayerId) Option
      boont : Boont
      score: Map<PlayerId,ScorePoint>}

  let empty:Model =
    { lastCard = None
      boont = NoBoont
      score = Map.empty }

  let boontScore = function
    | NoBoont -> 0
    | Boont -> 1
    | Hamsa -> 5
    | Ashra -> 10

  let addCard model (card,playerId) =
    match model.lastCard with
    | Some (pervCard,prevPlayerId) when pervCard.value = card.value ->
      let boont' =
        match model.boont with
        | NoBoont -> Boont
        | Boont -> Hamsa
        | Hamsa -> Ashra
        | Ashra -> NoBoont

      let scorePoint =
        match (model.boont, boont') with
        | (boont,NoBoont) -> boontScore boont
        | _ -> 0

      let score' =
        model.score |> Map.add prevPlayerId scorePoint

      { model with
          score = score'
          lastCard = Some (card,playerId)
          boont = boont' }
    | _ ->
      { model with
          lastCard = Some (card,playerId)
          boont = NoBoont }

module Ronda =
  type Ronda =
    | NoRonda
    | Ronda of int
    | Tringla of int

  type Model =
    { claiming: PlayerId list
      rondot: (PlayerId*Ronda) list }

  let rondaScore = function
  | NoRonda -> 0
  | Ronda _ -> 1
  | Tringla _ -> 5

  let getRonda cards =
    cards
    |> List.map cardValue
    |> List.sortBy ((*)-1)
    |> List.groupBy id
    |> List.map
      (fun (id,cs) ->
        match cs with
        | [v;_] -> Ronda v
        | [v;_;_] -> Tringla v
        | _ -> NoRonda
      )
    |> List.sortBy (rondaScore>>((*)-1))
    |> List.head


  let create (playerCards: PlayerCards list) =
    let rondot =
      playerCards
      |> List.map
        (fun (id,cards) -> id,(getRonda cards))

    { claiming = List.empty
      rondot = rondot }

type GameState =
  { board : Board.Model
    turn : Turn.Model
    boonts : Boonts.Model
    ronda : Ronda.Model
    scores : unit }

type Action =
  | PlayCard of int * int
